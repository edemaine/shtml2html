fs := require 'fs'
path := require 'path'
strftime := require 'strftime'
class Convert
  config: Record<string, string>
  vars: Record<string, string | (() => string)>
  rootFilename: string

  @(@rootFilename: string, outFilename: string)
    stat := fs.statSync @rootFilename
    @config =
      echomsg: '(none)'
      errormsg: '[an error occurred while processing this directive]'
      sizefmt: 'bytes'
      timefmt: "%A, %d-%b-%Y %H:%M:%S %Z"
    @vars =
      DOCUMENT_NAME: outFilename
      LAST_MODIFIED: =>
        strftime @config.timefmt, stat.mtime

  convertToString(filename: string = @rootFilename)
    html .= fs.readFileSync filename, encoding: 'utf8'
    loop
      old := html
      html = html
      .replace /<!--#include\s+virtual\s*=\s*"([^"]+)"\s*-->/g,
        (match: string, url: string) =>
          @convertToString path.join path.dirname(filename), url
      .replace /<!--#config\s+(\w+)="([^"]+)"\s*-->/g,
        (match: string, key: string, value: string) =>
          @config[key] = value
          ''
      .replace /<!--#echo\s+var="([^"]+)"\s*-->/g,
        (match: string, key: string) =>
          val .= @vars[key] ?? @config.echomsg
          val = val() if val instanceof Function
          val
      break if old == html
    html

function convertFile(filename: string)
  outFilename := filename.replace(/\.shtml$/, '') + '.html'
  console.log filename, '->', outFilename
  convert := new Convert filename, outFilename
  fs.writeFileSync outFilename, """
    <!--This file is automatically generated by shtml2html. DO NOT EDIT.-->
    #{convert.convertToString()}
  """, encoding: 'utf8'

function main()
  for arg of process.argv[2..]
    convertFile arg

main() if require.main == module
